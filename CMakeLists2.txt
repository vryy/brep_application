set(CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH} "${CMAKE_CURRENT_SOURCE_DIR}/custom_cmake_modules")

set(BREP_APPLICATION_COMPILE_DEFS "")
set(BREP_APPLICATION_INCLUDE_DIRS "")

find_package(MathPresso QUIET)
if(${MATHPRESSO_FOUND} MATCHES TRUE)
    message("mathpresso is found at " ${MATHPRESSO_DIR})
    set(BREP_APPLICATION_COMPILE_DEFS "${BREP_APPLICATION_COMPILE_DEFS};BREP_APPLICATION_USE_MASHPRESSO")
else()
    message("mathpresso is not found")
endif()

find_package(OpenCasCade QUIET)
if(${OCC_FOUND} MATCHES TRUE)
    message("BRepApplication uses OpenCasCade with include directory:" ${OCC_INCLUDE_DIR})
    message("BRepApplication uses OpenCasCade with libraries:" ${OCC_LIBRARIES})
    set(BREP_APPLICATION_INCLUDE_DIRS "${BREP_APPLICATION_INCLUDE_DIRS};${OCC_INCLUDE_DIR}")
    set(BREP_APPLICATION_COMPILE_DEFS "${BREP_APPLICATION_COMPILE_DEFS};BREP_APPLICATION_USE_OPENCASCADE")
else()
    message("OpenCasCade is not found")
endif()

#FIND_PACKAGE(CityGML QUIET)
#IF(${CITYGML_FOUND} MATCHES TRUE)
#    message("CityGML libraries:" ${CITYGML_LIBRARIES})
#    include_directories(${CITYGML_INCLUDE_DIR})
#    ADD_DEFINITIONS(-DBREP_APPLICATION_USE_CITYGML)
#ELSE()
#    message("CityGML is not found")
#ENDIF()

find_package(CGAL QUIET)
if(CGAL_FOUND)
    # SET(CGAL_DIR ${CGAL_ROOT}/lib/CGAL)
    set(BREP_APPLICATION_COMPILE_DEFS "${BREP_APPLICATION_COMPILE_DEFS};BREP_APPLICATION_USE_CGAL")
    set(BREP_APPLICATION_COMPILE_DEFS "${BREP_APPLICATION_COMPILE_DEFS};BREP_APPLICATION_USE_GMP")
    set(BREP_APPLICATION_INCLUDE_DIRS "${BREP_APPLICATION_INCLUDE_DIRS};${CGAL_INCLUDE_DIR}")
    message(STATUS "CGAL is found at " ${CGAL_ROOT} " and enabled for BRepApplication")
else()
    message(STATUS "CGAL is not found and linked with BRepApplication")
endif()

#######################################################################

######### add compilation units here
set( KRATOS_BREP_APPLICATION_SOURCES
    # main application compilation unit
    ${CMAKE_CURRENT_SOURCE_DIR}/brep_application_variables.cpp
    ${CMAKE_CURRENT_SOURCE_DIR}/brep_application.cpp
    ${CMAKE_CURRENT_SOURCE_DIR}/custom_algebra/brep.cpp
    ${CMAKE_CURRENT_SOURCE_DIR}/custom_algebra/curve/curve.cpp
    ${CMAKE_CURRENT_SOURCE_DIR}/custom_utilities/brep_mesh_utility.cpp
    ${CMAKE_CURRENT_SOURCE_DIR}/custom_python/add_custom_algebra_to_python.cpp
    ${CMAKE_CURRENT_SOURCE_DIR}/custom_python/add_transformation_to_python.cpp
    ${CMAKE_CURRENT_SOURCE_DIR}/custom_python/add_brep_and_level_set_to_python.cpp
)

if(${OCC_FOUND} MATCHES TRUE)
set( KRATOS_BREP_APPLICATION_SOURCES ${KRATOS_BREP_APPLICATION_SOURCES}
    ${CMAKE_CURRENT_SOURCE_DIR}/custom_utilities/MakeSphere.cxx
    ${CMAKE_CURRENT_SOURCE_DIR}/custom_utilities/MakeBottle.cxx
    ${CMAKE_CURRENT_SOURCE_DIR}/custom_utilities/ReadSTEP.cxx
    ${CMAKE_CURRENT_SOURCE_DIR}/custom_utilities/WriteSTEP.cxx
    ${CMAKE_CURRENT_SOURCE_DIR}/custom_python/add_occ_to_python.cpp
)
endif()

set( KRATOS_BREP_APPLICATION_SOURCES ${KRATOS_BREP_APPLICATION_SOURCES}
    ${CMAKE_CURRENT_SOURCE_DIR}/custom_python/add_utilities_to_python.cpp
    ${CMAKE_CURRENT_SOURCE_DIR}/custom_python/brep_python_application.cpp
)
#######################################################################

## define library Kratos which defines the basic python interface
add_library(KratosBRepApplication SHARED ${KRATOS_BREP_APPLICATION_SOURCES})
if(MSVC)
    target_compile_options(KratosBRepApplication PRIVATE /bigobj)
endif()

add_library(KratosBRepApplicationDependencies INTERFACE)
if(${MATHPRESSO_FOUND} MATCHES TRUE)
    target_link_libraries(KratosBRepApplicationDependencies INTERFACE ${MATHPRESSO_LIBRARIES})
endif()
if(${OCC_FOUND} MATCHES TRUE)
    target_link_libraries(KratosBRepApplicationDependencies INTERFACE ${OCC_LIBRARIES})
endif()
if(${CGAL_FOUND})
    target_link_libraries(KratosBRepApplicationDependencies INTERFACE ${CGAL_LIBRARIES})
    target_link_libraries(KratosBRepApplicationDependencies INTERFACE ${GMP_LIBRARIES})
endif()

target_link_libraries(KratosBRepApplication PUBLIC KratosCore)
target_link_libraries(KratosBRepApplication PUBLIC KratosBRepApplicationDependencies)

target_include_directories(KratosBRepApplication PUBLIC ${BREP_APPLICATION_INCLUDE_DIRS})
target_include_directories(KratosBRepApplication PUBLIC ${CMAKE_CURRENT_SOURCE_DIR})

target_compile_definitions(KratosBRepApplication PRIVATE KRATOS_CORE=IMPORT)
target_compile_definitions(KratosBRepApplication PRIVATE BREP_APPLICATION=EXPORT)
target_compile_definitions(KratosBRepApplication PUBLIC ${BREP_APPLICATION_COMPILE_DEFS})

set_target_properties(KratosBRepApplication PROPERTIES PREFIX "")
install(TARGETS KratosBRepApplication DESTINATION libs )
#######################################################################

## changing the .dll suffix to .pyd for Windows compatibility
if(${CMAKE_SYSTEM_NAME} MATCHES "Windows")
    set_target_properties(KratosBRepApplication PROPERTIES SUFFIX .pyd)
endif(${CMAKE_SYSTEM_NAME} MATCHES "Windows")
#######################################################################

## install application's python script if required
if(${INSTALL_PYTHON_FILES} MATCHES ON)
  get_filename_component (CURRENT_DIR_NAME ${CMAKE_CURRENT_SOURCE_DIR} NAME)
  install(DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}/python_scripts DESTINATION applications/${CURRENT_DIR_NAME}  FILES_MATCHING PATTERN "*.py"  PATTERN ".svn" EXCLUDE)
endif(${INSTALL_PYTHON_FILES} MATCHES ON)
#######################################################################

## Add to the KratosMultiphysics Python module
install(FILES "${CMAKE_CURRENT_SOURCE_DIR}/BRepApplication.py" DESTINATION KratosMultiphysics )
install(FILES ${MATHPRESSO_LIBRARIES} DESTINATION libs )
#######################################################################
